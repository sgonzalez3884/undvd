#!/usr/bin/perl
#
# Author: Martin Matusiak <numerodix@gmail.com>
# Licensed under the GNU Public License, version 3.

use strict;
use Getopt::Long qw(:config no_ignore_case);

use FindBin qw($RealBin);
use lib $RealBin;
use colors;
use common qw(:DEFAULT $suite $defaults $tools);


my $usage = "Usage:  " . s_b($suite->{tool_name}) . " "
	. s_bb("<file(s)>")
	. " [" . s_b("options") . "]
  <file(s)>     files to encode\n
     --start    start after this many seconds (usually for testing)
  -e --end      end after this many seconds (usually for testing)\n
  -C            do sanity check (check for missing tools)
  -z --adv      <show advanced options>
     --version  show " . $suite->{suite_name} . " version\n";

my $adv_usage = "Advanced usage:  " . s_b($suite->{tool_name}) . " "
	. s_bb("<file(s)>")
	. " [" . s_b("options") . "]
  -o --size     output file size in mb (integer value)
     --bpp      bits per pixel (float value)
  -1            force 1-pass encoding
  -2            force 2-pass encoding
  -c --crop     autocrop video
  -r --scale    scale video to x:y (integer value or " . s_bb("0") . ") or " . s_bb("off") . " to disable
  -f --smooth   use picture smoothing filter
  -D --dryrun   dry run (display encoding parameters without encoding)\n
     --cont     set container format
     --acodec   set audio codec
     --vcodec   set video codec\n";

my ($opts_start, $opts_end, $target_size, $bpp, $autocrop);
my ($dry_run, $opts_acodec, $opts_vcodec, $opts_cont);

my $custom_scale = "off";
my $target_passes = 1;
my $prescale = $defaults->{prescale};
my $postscale = $defaults->{postscale};

my $parse = GetOptions(
	"start=f"=>\$opts_start,
	"e|end=f"=>\$opts_end,

	"C"=> sub { init_cmds(1); exit; },
	"z|adv"=> sub { print $adv_usage; exit; },
	"version"=>\&print_version,

	"o|size=i"=>\$target_size,
	"bpp=f"=>\$bpp,
	"1"=> sub { $target_passes = 1; },
	"2"=> sub { $target_passes = 2; },
	"c|crop"=> sub { $autocrop = 1; },
	"r|scale=s"=>\$custom_scale,
	"f|smooth"=> sub { $prescale .= "spp,"; $postscale = ",hqdn3d$postscale"; },
	"D|dryrun"=> sub { $dry_run = 1; },

	"cont=s"=>\$opts_cont,
	"acodec=s"=>\$opts_acodec,
	"vcodec=s"=>\$opts_vcodec,
);

print_tool_banner();

if (! $parse) {
	print $usage;
	exit 2;
}

my @startpos = ("-ss", $opts_start ? $opts_start : 0);
my @endpos;
if ($opts_end) {
	push(@endpos, "-endpos", $opts_end);
}

my @files = @ARGV;
if (scalar @files < 1) {
	nonfatal("No files to encode, exiting");
	print $usage;
	exit 2;
}


init_logdir();


# Set container and codecs

my $container = $opts_cont ? $opts_cont : $defaults->{container};
my ($audio_codec, $video_codec, $ext, @cont_args) = set_container_opts($opts_acodec,
	$opts_vcodec, $container);

print " - Output format :: "
	. "container: " . s_it($container)
	. "  audio: "   . s_it($audio_codec)
	. "  video: "   . s_it($video_codec) . "\n";


# Display dry-run status

if ($dry_run) {
	print " * Performing dry-run\n";
	print_title_line(1);
}

foreach my $file (@files) {

	if (! -e $file) {
		nonfatal("File %%%$file%%% does not exist");
		next;
	} elsif (-d $file) {
		nonfatal("%%%$file%%% is a directory");
		next;
	}

	my $title_name = $file;
	$title_name =~ s/^(.*)\..*$/$1/g;


	# Display encode status

	if (! $dry_run) {
		print " * Now encoding file " . s_bb(trunc(38, 1, $file, "..."));
		if ($opts_start and $opts_end) {
			print "  [" . s_bb($opts_start) . "s - " . s_bb($opts_end) . "s]";
		} elsif ($opts_start) {
			print "  [" . s_bb($opts_start) . "s -> ]";
		} elsif ($opts_end) {
			print "  [ -> " . s_bb($opts_end) . "s]";
		}
		print "\n";
	}


	# Extract information from the title

	my $title = examine_title($file);

	# Init encoding target info

	my $ntitle = copy_hashref($title);
	$ntitle->{aformat} = $audio_codec;
	$ntitle->{vformat} = $video_codec;
	$ntitle->{filename} = "$title_name.$ext";


	# Do we need to crop?

	my $crop_arg;
	if ($autocrop) {
		my $est = get_crop_eta($ntitle->{length}, $ntitle->{fps});
		print " + Finding out how much to crop... (est: ${est}min)\r";
		my ($width, $height);
		($width, $height, $crop_arg) = crop_title($file);
		if (! $width or ! $height or ! $crop_arg) {
			fatal("Crop detection failed");
		}
		$ntitle->{width} = $width;
		$ntitle->{height} = $height;
	}

	# Find out how to scale the dimensions

	my ($width, $height) =
		scale_title($ntitle->{width}, $ntitle->{height}, $custom_scale);
	$ntitle->{width} = $width;
	$ntitle->{height} = $height;
	my $scale_arg = "scale=$width:$height";

	# Estimate filesize of audio

	$ntitle->{abitrate} = set_acodec_opts($container, $ntitle->{aformat},
		$ntitle->{abitrate}, 1);
	my $audio_size = compute_media_size($ntitle->{length}, $ntitle->{abitrate});
	my @acodec_args = set_acodec_opts($container, $ntitle->{aformat},
		$ntitle->{abitrate});

	# Decide bpp

	if ($bpp) {
		$ntitle->{bpp} = $bpp;
	} elsif ($target_size) {
		my $video_size = $target_size - $audio_size;
		$video_size = 1 if $video_size <= 0;
		$ntitle->{bpp} = compute_bpp($ntitle->{width}, $ntitle->{height},
			$ntitle->{fps}, $ntitle->{length}, $video_size);
	} else {
		$ntitle->{bpp} = set_bpp($video_codec, $target_passes);
	}

	# Reset the number of passes based on the bpp

	if ($target_passes) {
		$ntitle->{passes} = $target_passes;
	} else {
		$ntitle->{passes} = set_passes($video_codec, $ntitle->{bpp});
	}

	# Compute bitrate

	$ntitle->{vbitrate} = compute_vbitrate($ntitle->{width},
		$ntitle->{height}, $ntitle->{fps}, $ntitle->{bpp});


	# Dry run

	if ($dry_run) {

		# Estimate output size

		if ($target_size) {
			$ntitle->{filesize} = $target_size;
		} else {
			my $video_size = compute_media_size($ntitle->{length},
				$ntitle->{vbitrate});
			$ntitle->{filesize} = int($video_size + $audio_size);
		}

		$ntitle->{filename} = "$title_name.$container";

		print_title_line(0, $title);
		print_title_line(0, $ntitle);


	# Encode video

	} else {

		for (my $pass = 1; $pass <= $ntitle->{passes}; $pass++) {
			my @vcodec_args = set_vcodec_opts($container, $ntitle->{vformat},
				$ntitle->{passes}, $pass, $ntitle->{vbitrate});

			my @args = (@startpos, @endpos);
			push(@args, "-vf", "${crop_arg}${prescale}${scale_arg}${postscale}");
			push(@args, "-oac", @acodec_args);
			push(@args, "-ovc", @vcodec_args);
			push(@args, "-of", @cont_args);

			run_encode(\@args, $file, $title_name, $ext, $ntitle->{length},
				$ntitle->{passes}, $pass);
		}

		if (-f "$title_name.$ext.partial") {
			rename("$title_name.$ext.partial", "$title_name.$ext");
		}

		if (-f "divx2pass.log") {
			unlink("divx2pass.log");
		}

		remux_container($title_name, $ext, $ntitle->{fps}, $container,
			$ntitle->{aformat}, $ntitle->{vformat});

	}

}
